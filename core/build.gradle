plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.coffee'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation, testImplementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

idea {
    module {
        testSources.from(sourceSets.intTest.java.srcDirs, sourceSets.intTest.resources.srcDirs)
    }
}

dependencies {
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.flywaydb:flyway-core'
    implementation project(':commons')

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.instancio:instancio-junit:2.6.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    intTestImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'

    intTestRuntimeOnly 'com.h2database:h2'


}

test {
    useJUnitPlatform()
}

tasks.register('intTest', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test

}

